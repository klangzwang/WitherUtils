plugins {
    id 'java-library'
    id 'eclipse'
    id 'net.neoforged.gradle.userdev' version '7.0.145'
}

repositories {
	mavenCentral()
	maven {
		url "https://www.cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		name "Jei"
		url "https://maven.blamejared.com/"
		content {
			includeGroup "mezz.jei"
		}
	}
	maven {
		name "K-4u Mod maven"
		url "https://modmaven.dev/"
		content {
			includeGroup "mcjty.theoneprobe"
			includeGroup "mekanism"
		}
	}
    maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
}

dependencies {
	implementation 'net.neoforged:neoforge:21.0.86-beta'
	implementation("maven.modrinth:athena-ctm:${athenaVersion}")	
	runtimeOnly("mekanism:Mekanism:${mekanism_version}")
}

base {
    archivesName = "${archive_base_name}-${mod_version}+mc${minecraft_version}"
}

version = project.mod_version
group = project.maven_group

java {
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sourceSets {
	api {
		compileClasspath = main.compileClasspath
		resources.srcDirs = []
	}
	main {
		compileClasspath += api.output
		runtimeClasspath += api.output
		resources {
			srcDir "src/generated/resources"
		}
	}
}

runs {

    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'

        modSource project.sourceSets.api
        modSource project.sourceSets.main
    }
	client {
        systemProperty 'forge.enabledGameTestNamespaces', 'witherutils'
		jvmArguments.addAll('-Xmx4G')
	}
	server {
		systemProperty 'forge.enabledGameTestNamespaces', 'witherutils'
        programArgument '--nogui'
	}
	data {
		programArguments = ['--mod', 'witherutils',
							'--all',
							'--output', file('src/generated/resources/').absolutePath,
							'--existing', file('src/main/resources').absolutePath]
	}
}

tasks.withType(JavaCompile).configureEach
{
    options.encoding = 'UTF-8'
}

jar {
	from sourceSets.api.output
}